def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.firebase.crashlytics'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(localProperties.getProperty("submon.keyStorePath"))
            storePassword localProperties.getProperty("submon.keyStorePassword")
            keyAlias localProperties.getProperty("submon.keyAlias")
            keyPassword localProperties.getProperty("submon.keyPassword")
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "net.chikach.submon"
        minSdkVersion 24
        compileSdk 33
        targetSdkVersion 33
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            manifestPlaceholders["appLinkHostName"] = "open.submon.app"
            manifestPlaceholders["applicationLabel"] = "Submon"
            minifyEnabled = true
            shrinkResources = true
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
            ndk {
                debugSymbolLevel = "FULL"
            }
        }
        profile {
            versionNameSuffix "-profile"
            manifestPlaceholders["appLinkHostName"] = "dev.open.submon.app"
            manifestPlaceholders["applicationLabel"] = "[P]Submon"
            applicationIdSuffix ".debug"
        }
        debug {
            versionNameSuffix "-debug"
            manifestPlaceholders["appLinkHostName"] = "dev.open.submon.app"
            manifestPlaceholders["applicationLabel"] = "[D]Submon"
            applicationIdSuffix ".debug"
        }
    }

    buildFeatures {
        viewBinding true
    }
    namespace 'net.chikach.submon'
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.activity:activity-ktx:1.8.1"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.browser:browser:1.5.0"
    implementation 'androidx.exifinterface:exifinterface:1.3.6'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    implementation platform('com.google.firebase:firebase-bom:30.5.0')
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-appcheck-playintegrity'
    implementation 'com.google.firebase:firebase-appcheck-debug'

//    def roomVersion = "2.4.2"
//    implementation "androidx.room:room-runtime:$roomVersion"
//    kapt "androidx.room:room-compiler:$roomVersion"
}
